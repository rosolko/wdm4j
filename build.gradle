import com.github.spotbugs.SpotBugsTask

import java.nio.charset.StandardCharsets

plugins {
    id "java-library"
    id "checkstyle"
    id "pmd"
    id "jacoco"
    id "com.github.ben-manes.versions" version "0.20.0"
    id "com.github.spotbugs" version "1.6.9"
    id "org.sonarqube" version "2.7"
}

group = "com.github.rosolko"
version = "1.0.0"
description = "WebDriver manager for java"

apply from: rootProject.file("gradle/code-quality.gradle")
apply from: rootProject.file("gradle/publish.gradle")

sourceCompatibility = JavaVersion.VERSION_1_8

ext {
    encoding = StandardCharsets.UTF_8.toString()
}

tasks.withType(JavaCompile) {
    options.incremental = true
    options.encoding = encoding
    options.compilerArgs += ["-Xlint"]
}

tasks.withType(Javadoc) {
    options.encoding = encoding
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    systemProperty("file.encoding", encoding)
    systemProperty("java.enums.logging.manager", "org.apache.logging.log4j.jul.LogManager")
    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
}

tasks.withType(SpotBugsTask) {
    excludeFilter = rootProject.file("config/spotbugs/excludeFilter.xml")
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
    jcenter()
}

dependencies {
    api(platform("org.apache.logging.log4j:log4j-api:2.11.2"))
    implementation("org.jsoup:jsoup:1.11.3")
    implementation("kr.motd.maven:os-maven-plugin:1.6.2")
    implementation("org.apache.commons:commons-compress:1.18")
    testImplementation("org.junit.jupiter:junit-jupiter:5.4.0")
    testRuntimeOnly("org.apache.logging.log4j:log4j-core")
    testRuntimeOnly("org.apache.logging.log4j:log4j-jul")
    testRuntimeOnly("org.apache.logging.log4j:log4j-slf4j-impl")
    testRuntimeOnly("com.lmax:disruptor:3.4.2")
}

wrapper {
    gradleVersion = "5.2.1"
}
