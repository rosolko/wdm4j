import com.github.spotbugs.SpotBugsTask

import java.nio.charset.StandardCharsets

plugins {
    id "java-library"
    id "checkstyle"
    id "pmd"
    id "jacoco"
    id "com.github.ben-manes.versions" version "0.21.0"
    id "com.github.spotbugs" version "1.7.1"
    id "org.sonarqube" version "2.7.1"
    id "com.jfrog.bintray" version "1.8.4"
}

group = "com.github.rosolko"
version = "1.0.3-SNAPSHOT"
description = "WebDriver manager for java"

apply from: rootProject.file("gradle/code-quality.gradle")
apply from: rootProject.file("gradle/publish.gradle")

sourceCompatibility = JavaVersion.VERSION_1_8

ext {
    encoding = StandardCharsets.UTF_8.toString()
    log4j2Version = "2.11.2"
}

tasks.withType(JavaCompile) {
    options.incremental = true
    options.encoding = encoding
    options.compilerArgs += ["-Xlint"]
}

tasks.withType(Javadoc) {
    options.encoding = encoding
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    systemProperty("file.encoding", encoding)
    systemProperty("java.enums.logging.manager", "org.apache.logging.log4j.jul.LogManager")
    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
}

tasks.withType(SpotBugsTask) {
    excludeFilter = rootProject.file("config/spotbugs/excludeFilter.xml")
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
    jcenter()
}

dependencies {
    api("org.apache.logging.log4j:log4j-api:$log4j2Version")
    implementation("org.jsoup:jsoup:1.11.3")
    implementation("kr.motd.maven:os-maven-plugin:1.6.2")
    implementation("org.apache.commons:commons-compress:1.18")
    testImplementation("org.assertj:assertj-core:3.12.2")
    testImplementation("org.junit.jupiter:junit-jupiter:5.5.0-M1")
    testRuntimeOnly("org.apache.logging.log4j:log4j-core:$log4j2Version")
    testRuntimeOnly("org.apache.logging.log4j:log4j-jul:$log4j2Version")
    testRuntimeOnly("org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version")
    testRuntimeOnly("com.lmax:disruptor:3.4.2")
}

wrapper {
    gradleVersion = "5.4.1"
}
