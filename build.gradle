import com.github.spotbugs.snom.SpotBugsTask

import java.nio.charset.StandardCharsets

plugins {
    id "java-library"
    id "checkstyle"
    id "pmd"
    id "jacoco"
    id "com.github.ben-manes.versions" version "0.38.0"
    id "com.github.spotbugs" version "4.7.1"
    id "org.sonarqube" version "3.2.0"
    id "com.jfrog.bintray" version "1.8.5"
}

group = "com.github.rosolko"
version = "1.1.3-SNAPSHOT"
description = "WebDriver manager for java"

apply from: rootProject.file("gradle/code-quality.gradle")
apply from: rootProject.file("gradle/publish.gradle")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

ext {
    encoding = StandardCharsets.UTF_8.toString()
    log4j2Version = "2.14.1"
}

tasks.withType(JavaCompile) {
    options.incremental = true
    options.encoding = encoding
    options.release = 8
    options.compilerArgs += ["-Xlint"]
}

tasks.withType(Javadoc) {
    options.encoding = encoding
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    systemProperty("file.encoding", encoding)
    systemProperty("java.enums.logging.manager", "org.apache.logging.log4j.jul.LogManager")
    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")
}

tasks.withType(SpotBugsTask) {
    excludeFilter = rootProject.file("config/spotbugs/excludeFilter.xml")
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api("org.apache.logging.log4j:log4j-api:$log4j2Version")
    implementation("org.jsoup:jsoup:1.13.1")
    implementation("kr.motd.maven:os-maven-plugin:1.7.0")
    implementation("org.apache.commons:commons-compress:1.20")
    testImplementation("org.assertj:assertj-core:3.19.0")
    testImplementation("org.junit.jupiter:junit-jupiter:5.7.1")
    testRuntimeOnly("org.apache.logging.log4j:log4j-core:$log4j2Version")
    testRuntimeOnly("org.apache.logging.log4j:log4j-jul:$log4j2Version")
    testRuntimeOnly("org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version")
    testRuntimeOnly("com.lmax:disruptor:3.4.4")
}

wrapper {
    gradleVersion = "6.8.3"
}
